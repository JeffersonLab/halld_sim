MACRO kk_mass_spectra attach=1 fill=1 plot=1
* - draw histograms with K+K- mass spectra

gr/set * 
igset * 
exec ~/.pawlogon.kumac
exec sizes_set
zone 2 2

if [fill]>0 then
 if [attach]>0 then
   clo 2
   h/fil 2 ../run/bggen_kpkm.nt 0 -x
 endif

 1dh 201 'K+K- effective mass, total' 180 1.1 2.9
 h/cop 201 202 'K+K- effective mass, s_sbar initial' 180 1.1 2.9
 h/cop 201 203 'K+K- effective mass, u_ubar initial' 180 1.1 2.9
 h/cop 201 204 'K+K- effective mass, K+K- initial' 180 1.1 2.9
 h/cop 201 206 'K+K- effective mass aux' 180 1.1 2.9
 h/cop 201 207 'K+K- effective mass aux' 180 1.1 2.9
 h/cop 201 208 'K+K- effective mass aux' 180 1.1 2.9
 1dh 211 'K+/K- Pz total' 100 0 10
 1dh 212 'K+/K- Pz s_sbar initial' 100 0 10
 1dh 213 'K+/K- Pz u_ubar initial' 100 0 10
 1dh 214 'K+/K- Pz K+K-   initial' 100 0 10
 1dh 220 'K+/K- Pz' 100 0 10
 1dh 222 'K+/K- Pz_s_sbar s_sbar initial' 100 0 10
 1dh 223 'K+/K- Pz_s_sbar s_sbar initial' 100 0 10

 nt/pl //lun2/9.efm_typ.f(11,12) ! ! ! -201
 nt/pl //lun2/9.efm_typ.f(11,12) abs(ityp(3,1))=3.and.abs(ityp(3,2))=3 ! ! -202
 nt/pl //lun2/9.efm_typ.f(11,12) abs(ityp(3,1))=2.and.abs(ityp(3,2))=2 ! ! -203
 nt/pl //lun2/9.efm_typ.f(11,12) abs(ityp(3,1))=321.and.abs(ityp(3,2))=321 ! ! -204

 nt/loop //lun2/9 part_kin.f(0,11,0,211)>0
 nt/loop //lun2/9 part_kin.f(0,11,0,220)>0
 h/op/add 211 220 211
 h/pl 211
 nt/loop //lun2/9 np>0.and.abs(ityp(3,1))=3.and.abs(ityp(3,2))=3.and.part_kin.f(0,11,0,212)>0
 nt/loop //lun2/9 np>0.and.abs(ityp(3,1))=3.and.abs(ityp(3,2))=3.and.part_kin.f(0,12,0,220)>0
 h/op/add 212 220 212
 h/pl 212

 nt/pl //lun2/9.pout(3,1) abs(ityp(3,1))=3.and.abs(ityp(3,2))=3 ! !  -222
 nt/pl //lun2/9.pout(3,2) abs(ityp(3,1))=3.and.abs(ityp(3,2))=3 ! !  -223
 h/op/add 222 223 224
 
endif

if [plot]=1 then
 opt nsta
 opt ndat
 opt utit
 set xmgl 2.3
 set xlab 1.7
 igset lwid 1
 zone 2 2
* gr/set htyp -3
 gr/set hcol 1104
 vmx=$HINFO(201,'MAX')
 vmx=$SIGMA([vmx]*1.2)
 h/se/max 201 [vmx]
 h/pl 201
 atit 'M(K^-!K^+!), GeV' 'Entries/10 MeV'
* gr/set htyp -3
 h/cop 204 206
 h/op/add 206 203 207
 h/op/add 207 202 208
 gr/set hcol 1202
 h/pl 208 s
 gr/set hcol 1303
 h/pl 207 s
 gr/set hcol 1505
 h/pl 206 s
* h/op/sub 210 203 210
* gr/set hcol 1105
* h/pl 210 s
 opt ntic
 null 0 1 0 1 sab
 opt tic
 xx=0.63
 yy=0.9
 igset txci 1
 igset chhe 0.45

 igset pmci 4 
 igset plci 4 
 igset lwid 6 
 igset chhe 0.35
 itx 0.03 [yy] '[g](8.5GeV)p"5#K^+!K^-!p' 
 igset chhe 0.45
 gr/set csiz 0.40
 gr/hpl/key [xx] [yy] 1 'All' ! L
 d=0.06
 yy=$SIGMA([yy]-[d])
 igset pmci 2 
 igset plci 2 
 gr/hpl/key [xx] [yy] 1 's sbar initial' ! L
 yy=$SIGMA([yy]-[d])
 igset pmci 3 
 igset plci 3 
 gr/hpl/key [xx] [yy] 1 'u ubar initial' ! L
 yy=$SIGMA([yy]-[d])
 igset pmci 5 
 igset plci 5 
 gr/hpl/key [xx] [yy] 1 'K^+!K^-! initial' ! L

endif

if [plot]=2 then
 opt nsta
 opt ndat
 opt utit
 set xmgl 2.5
 set xlab 2
 igset lwid 1
 zone 2 2
* gr/set htyp -3
 gr/set hcol 1
 h/pl 211
 atit 'P?Z!(K^-! or K^+!), GeV' 'Entries/100 MeV'
* gr/set htyp -3
 igset txci 4
 igset chhe 0.35
 itx 3.5 15000. '[f](1020)' 
 opt ntic
 null 0 1 0 1 sab
 opt tic
 xx=0.63
 yy=0.9
 igset txci 1
 igset chhe 0.35
 itx 0.55 0.9 '[g](8.5GeV)p"5#K^+!K^-!p' 
 itx 0.7 0.80 'All K^+!K^-!' 

endif

if [plot]=3 then
 opt nsta
 opt ndat
 opt utit
 set xmgl 2.5
 set xlab 2
 igset lwid 1
 zone 2 2
* gr/set htyp -3
 gr/set hcol 4
 h/pl 224
 atit 'P?Z!, GeV' 'Entries/100 MeV'
* gr/set htyp -3
 gr/set hcol 2
 h/pl 212 s
 igset txci 4
 igset chhe 0.35
* itx 3.5 15000. '[f](1020)' 
 opt ntic
 null 0 1 0 1 sab
 opt tic
 xx=0.63
 yy=0.9
 igset txci 1
 igset chhe 0.35
 xx=0.15
 yy=0.9
 d=0.08
 itx [xx] [yy] '[g](8.5GeV)p"5#K^+!K^-!p' 
 yy=$SIGMA([yy]-[d])
 xx=0.25
 itx [xx] [yy] 's sbar initial state' 
 gr/set csiz 0.40
 yy=$SIGMA([yy]-[d])
 igset pmci 4
 igset plci 4 
 igset lwid 6 
 igset chhe 0.35
 gr/hpl/key [xx] [yy] 1 'P?Z!(s,sbar)' ! L
 igset pmci 2
 igset plci 2 
 yy=$SIGMA([yy]-[d])
 gr/hpl/key [xx] [yy] 1 'P?Z!(K^+!,K^-!)' ! L

endif

RETURN




